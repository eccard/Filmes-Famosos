apply plugin: 'com.android.application'

apply from: rootProject.file("dependencies.gradle")

android {
    compileSdkVersion compileVersion
    defaultConfig {
        applicationId "com.example.eccard.filmesfamosos"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {

        def TYPE_STRING = "String"
        def THEMOVIEDB_API_KEY = "THEMOVIEDB_API_KEY"
        def VAR_NAME_BASE_URL = "BASE_URL"

        def BASE_URL = "https://api.themoviedb.org/3/"

        def CONFIG = { k -> "\"${System.getProperty(k)}\"" }

        debug {
            buildConfigField TYPE_STRING, VAR_NAME_BASE_URL, "\"" + BASE_URL + "\""
            buildConfigField TYPE_STRING, THEMOVIEDB_API_KEY, CONFIG(THEMOVIEDB_API_KEY)
        }

        release {
            buildConfigField TYPE_STRING, VAR_NAME_BASE_URL, "\"" + BASE_URL + "\""
            buildConfigField TYPE_STRING, THEMOVIEDB_API_KEY, CONFIG(THEMOVIEDB_API_KEY)
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation(
            supportLibraries.values(),
            room.runtime,
            picasso.values(),
            retrofit.values(),
            okhttp3.values(),
            rx.values(),
            lifecycle.extensions


    )

    annotationProcessor(
            room.compiler,
            lifecycle.compiler,
    )
}
